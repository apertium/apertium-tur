DELIMITERS = "<.>" "<!>" "<?>" ;
SOFT-DELIMITERS = "<,>" ;

SUBREADINGS = RTL ; # Alternate, right-to-lfet (main reading on the right)

LIST BOS = (>>>) sent ; # Beginning of sentence
LIST EOS = (<<<) sent ; # End of sentence

LIST N = n ; 
LIST V = v ; 
LIST Prop = np ; 
LIST Pron = prn ; 
LIST Num = num ; 
LIST A = adj ; 
LIST Det = det ; 
LIST Adv = adv ; 
LIST CC = cnjcoo ; 
LIST CS = cnjsub ; 
LIST Interj = ij ; 
LIST Post = post ; 
LIST Cop = cop ; 

LIST Tv = (v tv) (v iv caus) (=tv) ;

LIST Pers = (prn pers) ;  # Use e.g. Sg3 + Pers to match pronouns
LIST Sg2 = (p2 sg) ;
LIST Pl3 = (p3 pl) ;
LIST Sg3 = (p3 sg) ;

LIST Px3Sp = px3sp ; 
LIST Px3Pl = px3pl ; 
LIST Px2Sg = px2sg ; 
LIST Px1Pl = px1pl ; 

LIST Nom = nom ; 
LIST Gen = gen ; 
LIST Acc = acc ; 
LIST Abe = abe ; 

LIST Subst = subst ; 
LIST Attr = attr ; 
LIST Advl = advl ; 

LIST Ant = (np ant);
LIST Cog = (np cog);

LIST Recip = rec ; 
LIST Caus = caus ; 
LIST Ref = ref ;

LIST Gpr = gpr_past gpr_fut gpr_rsub gpr_aor ;
LIST Gna = gna_aor ;
LIST Prc = prc ;
LIST Ger = ger ger1 ger2 ger3 ger_aor ger_fut ger_abst ger_past ;
LIST Finite = evid ifi past aor pres fut ;

LIST Imper = imp ; 

SET PRE-N = Det | Num | Attr | A | Gen | ("-") ; # CC 

SET NOMINAL-HEAD = N | Ger | Subst ; 

SET WORD = N | V | A | Post | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;
SET MARK =  (",") | ("\\") | ("\;") ; #"
SET PHRASEMARK =  ("\\") | ("\;") ; #"
SET COMMA = (",") ;
SET WORDMARK = WORD | MARK ;

SET NPNHA = WORDMARK - PRE-N - Adv ;


SECTION

#
## Hacks for subreadings 
#

ADD (=tv) (*) IF ((0 Tv) OR (0/1 Tv)) (NOT 0 (=tv)) ;

#
## Remove productive derivations when there are lexicalised alternatives
#

REMOVE V + Recip IF (0 V - Recip) ; 
    ## Samaras liderliğindeki koalisyon, PASOK ve küçük Demokratik Sol tarafından oluşmasına karşın, ...

REMOVE V + Caus IF (0 V - Caus) ; 
    ## ... , yan emekli aylıklarının kaldırılmasını içermesi bekleniyor.

REMOVE N + Abe IF (0 A) ; 
    ## , yaklaşık 2 milyon kişiyi işsiz bırakan ve ekonomiyi yüzde 7 oranında küçülten tasarruf tedbirlerinin devamı yönündeki ısrarına karşı direniyor.

#
## Unlikely readings can be discarded, we can write rules to select these in certain contexts.
#

SELECT Adv IF (0 ("yarın")) ;
    ## Yarın gelir misin?

REMOVE Cog + Px2Sg IF (0 Cog + Gen) ; 
    ## [0]Cinciç'in anısına Belgrad'da binlerce kişi toplandı.

REMOVE Imper IF (NOT 0C Imper) ;
    ## Bunlar, bu hafta içinde dünyanın dört bir yanından İtalyan başkentine [0]akın etmiş olan yaklaşık dört milyon hacı ve yaslı kalabalığa katıldı.

SELECT Det IF
	(1 N) ;
	# [0]bazı şeyleri farklı yapardık
	## needs to be in this section so that acc/px3sp stuff doesn't happen

SECTION 

REMOVE Cop IF
	(1 N)
	(NOT 1 EOS) ;
    ## Eczanelerde satılan [0]ilaçlar da bu alanda rağbet görüyor.

REMOVE Cop IF
	(0 N)
	(NOT 1 EOS) ;

REMOVE Pron + Nom IF (0 Det) (1C N) ;
    ## Bunlar, [0]bu hafta içinde dünyanın dört bir yanından İtalyan başkentine akın etmiş olan yaklaşık dört milyon hacı ve yaslı kalabalığa katıldı.

REMOVE Num + Nom IF (1C Num) ; 
    ## Bu araba [0]11,5 milyar avroluk.

REMOVE Attr IF (NOT 1 PRE-N) (NOT 1 NOMINAL-HEAD) (NOT 1/1 NOMINAL-HEAD) ; 
    ## Bu araba 11,5 milyar avroluk.

SELECT Adv IF (0 ("en")) (1 A) ; 
    ## En az 11,5 milyar avroluk ve ...

REMOVE Subst + Nom IF (1* N + Nom BARRIER NPNHA)  ; 
    ## En az 11,5 milyar avroluk ve ...

REMOVE N + Nom IF (0 Attr OR Nom) (1C Nom) ;
    ## Samaras'ın AB-IMF-ECB troykası tarafından talep edilen ve krizi daha da derinleştiren, ...

SELECT Attr (0 Nom) (1C Px3Sp + Nom) ; 
    ## Girit Üniversitesi ekonomi öğretim üyesi ve ...

REMOVE Post IF (-1 BOS) ; 
    ## Sonra arkadaşlarımı evde güzel bir öğle yemeğine davet eder, onlara ne kadar değer verdiğimi anlatırdım.

REMOVE A + Subst IF (1 ("bir") LINK 1C N); 
    ## Sonra arkadaşlarımı evde güzel bir öğle yemeğine davet eder, onlara ne kadar değer verdiğimi anlatırdım.

SELECT Det (0 ("bir")) (-1C A) (1C N); 
    ## Sonra arkadaşlarımı evde güzel bir öğle yemeğine davet eder, onlara ne kadar değer verdiğimi anlatırdım.

SECTION

SELECT Post IF
	(-1C N)
	## hayat dolu

SELECT Finite IF
	(0 Ger)
	(1 EOS) ;
	# as opposed to Ger readings

# select finite subreading at the end of a sentence
SELECT Sub:1 Finite IF
	#(0/1 Ger)                # FIXME: why does this only work when commented
	(1 EOS OR PHRASEMARK) ;
	# Hayattaki son dersi "kendi ölümü" [0]olacakmış.

REMOVE N IF
	(0 Finite)
	(1 MARK) ;
	# kahvaltı eder

REMOVE Px2Sg IF (NOT -1* Sg2 BARRIER N) ;
	# 1994'te vücudunda bir gariplik hissetmiş.

REMOVE Px3Pl IF (NOT -1* Pl3 BARRIER N) ;
	# 60'lık vücudu artık dans _derslerini_ kaldıramayacak kadar bitkinleşmiş.

REMOVE V IF
	(0 ("yemek") + N)
	(1 ("ye") + V) ;
	# yemek yer ...

#SELECT the non Px* reading IF
#	(0 Px3Sg)
#	(NOT (-1* (Sg3 + Gen) OR (N + Gen)) BARRIER N) ;
#	# Sonra arkadaşlarımı evde güzel bir öğle yemeğine davet ederim
#	# ^öğle yemeğine/öğle yemeği<n><dat>/öğle yemeği<n><px2sg><dat>/öğle yemeği<n><px3sp><dat>$

#REMOVE Acc IF
#	(0 Px3Sp)
#	(NOT 1* Tv BARRIER V) ;

REMOVE Acc IF
	(0C Px3Sp OR Acc)
	(
		(1C* Acc BARRIER Tv) 
		#  60'lık [0]vücudu artık dans derslerini kaldıramayacak kadar bitkinleşmiş.
		OR
		(NOT 1C* Tv BARRIER EOS)
	);

REMOVE Sub:1 Acc IF
	(0C/1 Px3Sp OR Acc)
	(
		(1C* Acc BARRIER Tv) 
		OR
		(NOT 1C* Tv BARRIER EOS)
		#  Mori Schwartz, hayat dolu bir üniversite [0]profesörüydü.
	);


SELECT N IF
	(0 V + Ger) ;

SELECT Gen IF
	(0 Cop)
	(0 ("biz"))
	(1* Px1Pl BARRIER EOS) ;
	# "bizim" not biz+i<cop><aor><p1><sg>

SELECT Advl IF
	(1 Cop LINK 0/1 V) ;

REMOVE Ger IF
	(0 Gpr)
	(1* N BARRIER (NOT PRE-N)) ;

SELECT Gna IF
	( (0 Sg3) OR (0/1 Sg3) )
	(1 COMMA) ;

SELECT SUB:1 Gna IF
	( (0 Sg3) OR (0/1 Sg3) )
	(1 COMMA) ;

SELECT Prc IF
	(
		(1 ("ol" v))
		OR
		(1/1 ("ol" v))
	);

SELECT Sub:1 ("düşün") IF
	(0/1 ("düş") + V + Ref) ;

SELECT ("düşün") IF
	(0 ("düş") + V + Ref) ;


AFTER-SECTIONS

SUBSTITUTE (=tv) (*) (*) ; 
