DELIMITERS = "<.>" "<!>" "<?>" ;
SOFT-DELIMITERS = "<,>" ;

LIST BOS = (>>>) sent ; # Beginning of sentence
LIST EOS = (<<<) sent ; # End of sentence

LIST N = n ; 
LIST V = v ; 
LIST Prop = np ; 
LIST Pron = prn ; 
LIST Num = num ; 
LIST A = adj ; 
LIST Det = det ; 
LIST Adv = adv ; 
LIST CC = cnjcoo ; 
LIST CS = cnjsub ; 
LIST Interj = ij ; 
LIST Post = post ; 
LIST Cop = cop ; 

LIST Px2Sg = px2sg ; 

LIST Nom = nom ; 
LIST Gen = gen ; 

LIST Subst = subst ; 
LIST Attr = attr ; 
LIST Advl = advl ; 

LIST Cog = (np cog);

LIST Ger = ger1 ger2 ger3 ; 

LIST Imper = imp ; 

SET PRE-N = Det | Num | Attr | A | Gen | ("-") ; # CC 

SET NOMINAL-HEAD = N | Ger | Subst ; 

SET WORD = N | V | A | Post | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;
SET MARK =  (",") | ("\\") | ("\;") ; #"
SET WORDMARK = WORD | MARK ;

SET NPNHA = WORDMARK - PRE-N - Adv ;

SECTION 

REMOVE Cog + Px2Sg IF (0 Cog + Gen) ; 
    ## [0]Cinciç'in anısına Belgrad'da binlerce kişi toplandı.

REMOVE Cop IF (NOT 1 EOS) ;
    ## Eczanelerde satılan [0]ilaçlar da bu alanda rağbet görüyor.

REMOVE Pron + Nom IF (0 Det) (1C N) ;
    ## Bunlar, [0]bu hafta içinde dünyanın dört bir yanından İtalyan başkentine akın etmiş olan yaklaşık dört milyon hacı ve yaslı kalabalığa katıldı.

REMOVE Imper IF (NOT 0C Imper) ;
    ## Bunlar, bu hafta içinde dünyanın dört bir yanından İtalyan başkentine [0]akın etmiş olan yaklaşık dört milyon hacı ve yaslı kalabalığa katıldı.

REMOVE Num + Nom IF (1C Num) ; 
    ## Bu araba [0]11,5 milyar avroluk.

REMOVE Attr IF (NOT 1 PRE-N) (NOT 1 NOMINAL-HEAD) ; 
    ## Bu araba 11,5 milyar avroluk.

SELECT Adv IF (0 ("en")) (1 A) ; 
    ## En az 11,5 milyar avroluk ve ...

REMOVE Subst + Nom IF (1* N + Nom BARRIER NPNHA)  ; 
    ## En az 11,5 milyar avroluk ve ...
