!! List of people who will hate this file:
!!     1. Firespeakerwh

Alphabet

 A B C Ç D E F G Ğ H I İ J K L M N O Ö P Q R S Ş T U Ü V W X Y Z 
 a b c ç d e f g ğ h ı i j k l m n o ö p q r s ş t u ü v w x y z 

 %{A%}:a %{I%}:ı %{D%}:d

 %{n%}:0 %{y%}:0 %{s%}:0 %{i%}:0

%{pN%}:0


 %>:0

 %+ %- %' % ;


Sets

SurVow = İ Ü E Ö I U A O
         i ü e ö ı u a o ; 


Vow    = İ Ü E Ö I U A O
         i ü e ö ı u a o
       %{A%} %{I%} ;

Cns    = B C Ç D F G Ğ H J K L M N P Q R S Ş T V W Y Z 
         b c ç d f g ğ h j k l m n p q r s ş t v w y z 
         %' ;

HardVow  = a ı o u 
           A I O U ;

SoftVow  = e i ö ü 
           E   Ö Ü ;

FlatVow  = a e i ı 
           A E   I ;

RoundVow = o u ö ü
           O U Ö Ü ;

Cnsdev   = f s t k ç ş h p 
           F S T K Ç Ş H P ;

Rules 

"Vowel harmony for archiphoneme {I}"
%{I%}:Vy <=> [ :LastVowel :Cns* :Cns ]/[ :0 | %>: ] _ ;
        where 
            LastVowel in ( i ü e ö ı u a o )
                   Vy in ( i ü i ü ı u ı u ) 
            matched;

! ev>{s}{I}         = evi      =     e:e v:v >:0 {s}:0 {I}:i
! akü>{n}{I}n       = akünün   = a:a k:k ü:ü >:0 {n}:n {I}:ü n:n

"Vowel harmony for archiphoneme {A}"
%{A%}:e <=> [ :SoftVow :Cns* :Cns ]/[ :0 | %>: ] _ ;

! ev>l{A}r          = evler     =     e:e v:v >:0 l:l {A}:e r:r

"Devoicing of archiphoneme {D} after Cnsdev"
%{D%}:t <=> :Cnsdev %>: _ ; 

! ağaç>{D}{A}n      = ağaçtan   = a:a ğ:ğ a:a ç:ç >:0 {D}:t {A}:a n:n
! fıstık>{D}{A}n    = fıstıktan = 
! şiş>{D}{A}n       = şişten    =
! gunah>{D}{A}n     = gunahtan  =
! kebap>{D}{A}n     = kebaptan  = 

! ev>{s}{I}         = evi       =     e:e v:v >:0 {s}:0 {I}:i
! akü>{n}{I}n       = akünün    = a:a k:k ü:ü >:0 {n}:n {I}:ü n:n

"Buffer N after 3rd singular possessive + dat/loc/abl"

%{pN%}:n <=>  %>: _  %{D%}: ; 

!arkasıNda
!memelerinden
!arabasında



"Epenthetic consonants after vowel"
Cx:Cy <=>   :SurVow %>: _ ; 
        where
            Cx in ( %{n%} %{s%} %{y%} ) 
            Cy in (   n     s     y   ) 
        matched ; 

! ev>{s}{I}         = evi      =      e:e v:v >:0 {s}:0 {I}:i
! ada>{s}{I}        = adası    =  a:a d:d a:a >:0 {s}:s {I}:ı
! adam>{n}{I}n      = adamın   = 
! akü>{n}{I}n       = akünün   = 
! kitap>{y}{I}      = kitabı   = 
! anne>{y}{I}       = anneyi   = 

"Prevocalic consonant voicing"
Cx:Cy <=> :Vow (:0) _ %>: (:0) :Vow ;
          except
                 :n _ %>: (:0) :Vow ;
          where Cx in ( p t k ç )
                Cy in ( b d ğ c )
                matched;

! kitap>{y}{I}      = kitabı   =
! tat>{y}{I}        = tadi     =
! fistik>{y}{I}     = fistiği  = 
! ağaç>{y}{I}       = ağacı    =
! renk>{y}{I}       ≠ renki    = 
! gönç>{y}{I}       ≠ göncü    =
! dekont>{y}{I}     ≠ dekondu  =

"Alternative voicing of 'k' to 'g' after 'n'"
k:g <=> :n _ %>: (:0) :Vow ;

! renk>{y}{I}       = rengi    =

