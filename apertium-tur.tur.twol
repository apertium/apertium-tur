Alphabet

   A Â B C Ç D E F G Ğ H I İ J K L M N O Ö P Q R S Ş T U Ü V W X Y Z 
   a â b c ç d e f g ğ h ı i j k l m n o ö p q r s ş t u ü v w x y z 

 %{A%}:a 
 %{I%}:ı 
 %{D%}:d 
 %{C%}:c
 %{L%}:l %{L%}:n ! For the passive 

 %{N%}:0 
 %{y%}:0 
 %{s%}:0 
 %{в%}:y

 %{i%}:0 
 %{a%}:0 
 %{u%}:0         ! Epenthetic vowel

 %{f%}:0 
 %{b%}:0

 %{n%}:0 %{n%}:n

 %{e%}:e

 %{z%}:z

 %{ii%}:0 %{o%}:0 %{ö%}:0 %{ı%}:0 %{p%}:0 %{b%}:0 ! For numerals and abbreviations / acronyms

 %{k%}:k ! ???
 
 %{v%}:0 ! Voicing

 %{fi%}:0 ! force front vowel harmony

 %>:0

 %%

 %+ %- %' % ;




Sets

SurVow = İ Ü E Ö I U A Â O
         i ü e ö ı u a â o ; 


Vow    = İ Ü E Ö I U A Â O
         i ü e ö ı u a â o
       %{A%} %{I%} %{e%} %{a%} %{i%} ;

Cns    = B C Ç D F G Ğ H J K L M N P Q R S Ş T V W Y Z 
         b c ç d f g ğ h j k l m n p q r s ş t v w y z 
         %' %{ç%} %{t%} ;

AbstrVow = %{fi%} %{ii%} %{ö%} %{o%} %{ı%} %{u%} ; 

HardVow  = a â ı o u 
           A Â I O U ;

SoftVow  = e i ö ü 
           E İ Ö Ü ;

FlatVow  = a â e i ı 
           A Â E İ I ;

RoundVow = o u ö ü
           O U Ö Ü ;

CnsDev   = f s t k ç ş h p 
           F S T K Ç Ş H P ;

AbstrCns = %{N%} %{y%} %{s%} %{в%} ;

Rules 

"Vowel harmony for archiphoneme {I}"
%{I%}:Vy <=> [ :LastVowel :Cns* :Cns ]/[ [ :0 - [ AbstrVow:0 - %{u%}: ] ] | %>: | %  ] _ ;
                                                       :LastVowel :Cns*  %'  %>: _ ;
        except
                                             %{e%}:0 %>: _ ;
                                         [ AbstrVow:0 - %{u%}: ] [ :0 | :Cns | :%' | % ]* _ ; 
                      [ %{z%}:0 %>: %{y%}: ]/:0 _ :m ;
        where 
            LastVowel in ( i ü e ö ı u a o â İ Ü E Ö I U A O Â )  ! This is horrible why do we need to do it with uppercase too
                   Vy in ( i ü i ü ı u ı u ı i ü i ü ı u ı u ı )  
            matched;

"Vowel harmony to abstract vowels for archiphoneme {I}"
!%{I%}:Vy <=> Vx:0 [ :0 | :Cns | :%' | % ]* _ ;
%{I%}:Vy <=> Vx:0 [ [ :0 - Vx: ] | :Cns | :%' | % ]* _ ;
                           Vx: %'  %>: _ ;
        except 
             [ AbstrVow: - [ Vx: | %{u%}: ] ] [ :0 | :Cns | :%' | % ]* _ ; 
                                              %{e%}:0 %>: _ ;
!                      [ %{u%}: ] [ :0 | :Cns | :%' | % ]* _ ; 
                                [ %{z%}:0 %>: %{y%}: ]/:0 _ ;
        where 
              Vx in ( %{fi%} %{ii%} %{o%} %{ö%} %{ı%} ) 
              Vy   in (  i      i     u     ü     ı   )  
               matched ;

!"Hidden vowel harmony for archiphoneme {I}:i"
!%{I%}:i <=> [ %{fi%}:0 | %{ii%}:0 ]/[ [ :0 - [ %{fi%}: | %{ii%}: ] ] | :Cns | :%' | % ]* _ ;
!        except 
!             [ AbstrVow: - [ %{fi%}: | %{ii%}: | %{u%}: ] ] [ :0 | :Cns | :%' | % ]* _ ; 
!                                              %{e%}:0 %>: _ ;
!
!"Hidden vowel harmony for archiphoneme {I}:u"
!%{I%}:u <=> [ %{o%}:0 ]/[ [ :0 - [ %{o%}: ] ] | :Cns | :%' | % ]* _ ;
!        except 
!             [ AbstrVow: - [ %{o%}: | %{u%}: ] ] [ :0 | :Cns | :%' | % ]* _ ; 
!                                              %{e%}:0 %>: _ ;
!
!"Hidden vowel harmony for archiphoneme {I}:ü"
!%{I%}:ü <=> [ %{ö%}:0 ]/[ [ :0 - [ %{ö%}: ] ] | :Cns | :%' | % ]* _ ;
!        except 
!             [ AbstrVow: - [ %{ö%}: | %{u%}: ] ] [ :0 | :Cns | :%' | % ]* _ ; 
!                                              %{e%}:0 %>: _ ;



! ev>{s}{I}           = evi      =     e:e v:v >:0 {s}:0 {I}:i
! akü>{N}{I}n         = akünün   = a:a k:k ü:ü >:0 {N}:n {I}:ü n:n
! 4{ö}{p}'>{I}nc{I}   = 4'üncü   = 4:4 {ö}:0 {p}:0 ':' >:0 {i}:ü n:n c:c {I}:ü
! 4{ö}{p}'>{N}{I}n    = 4'ün     = 4:4 {ö}:0 {p}:0 ':' >:0 {N}:0 {I}:ü n:n

"Special harmony for {e}"
%{I%}:i <=> %{e%}:0 %>: _ ; 

"Delete %{I%} after %{z%} in p1.sg aorist form (but leave in p1.pl)"
%{I%}:0 <=> [ %{A%}: %{z%}:0 %>: %{y%}: ]/:0 _ :m ;
        except
            %{e%}:0 %>: _ ;

"Delete %{z%} in aorist forms with %{y%}"
%{z%}:0 <=> %{A%}: _ [ %>: %{y%}: ]/:0 ;

"Special harmony for {e} 2 "
%{e%}:i <=>  _ %>: %{y%}: ; 


! 
! d{e}>{I}yor         = diyor      =                                 d:d {e}:0 >:0 {I}:i y:y o:o r:r
! NATO{o}'>{N}{I}n    = NATO'nun   =           N:N A:A T:T O:O {o}:0 ':' >:0 {N}:n {I}:u n:n
! IMF{i}'>{N}{I}n     = IMF'nin    =               I:I M:M F:F {i}:0 ':' >:0 {N}:n {I}:i n:n
! PKK{ı}'>{N}{I}n     = PKK'nın    =               P:P K:K K:K {ı}:0 ':' >:0 {N}:n {I}:ı n:n
! KFOR{o}{r}>{N}{I}n  = KFOR'un    =     K:K F:F O:O R:R {o}:0 {r}:0 ':' >:0 {N}:0 {I}:u n:n
! PASOK{o}{r}>{N}{I}n = PASOK'un   = P:P A:A S:S O:O K:K {o}:0 {r}:0 ':' >:0 {N}:0 {I}:u n:n

"Vowel {i} surfacing and harmonising"
%{i%}:Vy <=>           [ :LastVowel :Cns* :Cns ]/[ [ :0 - [ AbstrVow:0 - %{u%}: ] ] | %>: | %  ] _ [ %{L%}: | :Cns ]/[ :0 ]  ;
             [ :LastVowel [ :Cns* | %>: ] :Cns ]/[ [ :0 - [ AbstrVow:0 - %{u%}: ] ] | %>: | %  ] _ [ %{L%}: | :Cns ]/[ :0 ]  ; ! oynatırım
        except
                                         [ AbstrVow:0 - %{u%}: ] [ :0 | :Cns | :%' | % ]* _ ; 
        where 
            LastVowel in ( i ü e ö ı u a o â )
                   Vy in ( i ü i ü ı u ı u ı ) 
            matched;

! yap>{i}yor            = yapıyor    =               y:y a:a p:p >:0 {i}:ı y:y o:o r:r
! yap>{D}{I}r>{i}r>{I}m = yaptırırım =   y:y a:a p:p >:0 {D}:t {I}:ı r:r >:0 {i}:ı r:r >:0 {I}:ı m:m
! oyna>{i}{L}>{I}yor>   = oynalıyor  =           o:o y:y n:n a:a >:0 {i}:0 {L}:l >:0 {i}:ı y:y o:o r:r 
! oyna>t>{i}r>{I}m      = oynatırım  =   o:o y:y n:n a:a >:0 t:t >:0 {i}:ı r:r >:0 {I}:ı m:m
! et{·}>{i}{L}>{y}{A}n  = edilen     =             e:e t:d {·}:0 >:0 {i}:i {L}:l >:0 {y}:0 {A}:e n:n
! yap>{i}{L}>{y}{A}n    = yapılan    =   y:y a:a p:p >:0 {i}:ı {L}:l >:0 {y}:0 {A}:a n:n

"Vowel harmony to abstract vowels for archiphoneme {i}"
%{i%}:Vy <=> Vx:0 [ [ :0 - Vx: ] | :Cns | :%' | % ]* _ ;
        except 
             [ AbstrVow: - [ Vx: | %{u%}: ] ] [ :0 | :Cns | :%' | % ]* _ ; 
                                              %{e%}:0 %>: _ ;
!                      [ %{u%}: ] [ :0 | :Cns | :%' | % ]* _ ; 
        where 
              Vx in ( %{fi%} %{ii%} %{o%} %{ö%} %{ı%} ) 
              Vy   in (  i      i     u     ü     ı   )  
               matched ;

!"Unepenthesis in stems"
!%{u%}:0 <=> _ [ :Cns :SurVow ]/[ :0 | %>: ] ;
!            _ :Cns %>: %{N%}: %{I%}: :n .#. ; 
!            _ :Cns %>: %{y%}: %{I%}: .#. ; 
!            _ :Cns %>: %{y%}: %{A%}: .#. ; 


! bur{u}n               = burun      =
! bur{u}n>{i}m          = burnum     =  
! şeh{u}r               = şehir      =
! şeh{u}r>{y}{I}        = şehri      =  
! nak{fi}{u}t           = nakit
! nak{fi}{u}t>{s}{I}    = nakti

!"Vowel {u} nominative epenthesis"
!%{u%}:Vy <=> [ :LastVowel :Cns* :Cns ]/[ :0 ] _ [ :Cns [ .#. | :Cns ] ]/[ :0 | %>:] ;
!        except 
!            _ :Cns %>: %{N%}: %{I%}: :n .#. ;
!            _ :Cns %>: %{y%}: %{I%}: .#. ;
!            _ :Cns %>: %{y%}: %{A%}: .#. ;
!
!        where 
!            LastVowel in ( i ü e ö ı u a o â )
!                   Vy in ( i ü i ü ı u ı u ı ) 
!            matched;

"High-vowel epenthesis, surfacing of {u} when not syllable-final"
%{u%}:Vy <=> [ :LastVowel :Cns* :Cns ]/[ :0 - [ %{fi%}: | %{u%}: ] ] _ [ :Cns [ .#. | :Cns ] ]/[ :0 | %>: ] ;
        where 
            LastVowel in ( i ü e ö ı u a o â )
                   Vy in ( i ü i ü ı u ı u ı ) 
            matched;

! bur{u}n               = burun      =
! bur{u}n>{i}m          = burnum     =  

"Irregular high-vowel epenthesis"
%{u%}:i <=> [ :Cns %{fi%}: ]/[ :0 - [ %{fi%}: | %{u%}: ] ] _ [ :Cns [ .#. | :Cns ] ]/[ :0 | %>: ] ;


! nak{fi}{u}t           = nakit
! nak{fi}{u}t>{s}{I}    = nakti


"Vowel {a} surfacing and harmonising"
%{a%}:Vy <=> [ :LastVowel :Cns* :Cns ]/[ %>: | :0 | %   ] _ %>: [ :Cns ]/[ :0 ]  ;
        except
                                                          _ %>: %{I%}:  ;
        where 
            LastVowel in ( i ü e ö ı u a o â )
                   Vy in ( e e e e a a a a a ) 
            matched;

! yap>{i}{L}>m{a}>{D}{I} = yapılmadı

"Vowel harmony for archiphoneme {A}"
%{A%}:e <=> [ :SoftVow :Cns* [ AbstrCns:0 | :Cns ] ]/[ :0 | %>: | %  ] _ ;
            [ %{f%}:   :Cns* [ AbstrCns:0 | :Cns ] ]/[ :0 | %>: | %  ] _ ;
       except
              _ %>: %{I%}: y:y o:o r:r ;
             [ %{ii%}:0 | %{ö%}:0 | %{ı%}:0 | %{o%}:0 ] [ :0 | :Cns | :%' | % ]* _ ;

"Underlying vowel harmony for archiphoneme {A}"
%{A%}:e <=> [ %{fi%}:0 | %{ii%}:0 | %{ö%}:0 ] [ :0 | :Cns | :%' | % ]* _ ;
        except 
              _ %>: %{I%}: y:y o:o r:r ;
             [ AbstrVow: - [ %{fi%}:0 | %{ii%}: | %{ö%}: | %{u%}:] ] [ :0 | :Cns | :%' | % ]* _ ; 
             [ :SoftVow :Cns* [ AbstrCns:0 | :Cns ] ]/[ :0 | %>: | %  ] _ ;
             [ %{f%}:   :Cns* [ AbstrCns:0 | :Cns ] ]/[ :0 | %>: | %  ] _ ;

! ev>l{A}r              = evler     =     e:e v:v >:0 l:l {A}:e r:r
! saat{f}>l{A}r         = saatler   =     s:s a:a a:a t:t {f}:0 >:0 l:l {A}:e r:r
! saat{f}>{y}{A}        = saate     =     s:s a:a a:a t:t {f}:0 >:0 {y}:0 {A}:e
! eve>{y}{A}            = eve       =     e:e v:v >:0 {y}:0 {A}:e
! kitab>y{A}            = kitaba    =     k:k i:i t:t a:a p:b >:0 {y}:0 {A}:a
! 4{ö}{p}'>{y}{A}       = 4'e       =       4:4 {ö}:0 {p}:0 ':' >:0 {y}:0 {A}:e
! ol>m{A}k              = olmak     =     o:o l:l >:0 {A}:a k:k
! SETimes{ı}{b}'>{y}{A} = SETimes'a =     S:S E:E T:T i:i m:m e:e s:s {ı}:0 {b}:0 ':' >:0 {y}:0 {A}:a

"Consonant voicing in verb stems"
Cx:Cy <=>  _ %>: [ :SurVow ]/[ :0 ] ;
        where
            Cx in ( %{t%} ) 
            Cy in (   d   ) 
        matched ; 
!
! e{t}>iyor          = ediyor


"Vowel deletion in verb stems"
Cx:Cy <=>  _ %>: [ :SurVow ]/[ :0 ] ;
        except
            _ %>: %{y%}: ;  ! avoid conflict with {e} 2 rule
        where
            Cx in ( %{e%} ) 
            Cy in (   0   ) 
        matched ; 
!
! d{e}>iyor          = diyor


"Devoicing of archiphoneme {D} after CnsDev"
Cx:Cy <=> [ %{p%}: | :CnsDev ] [ [ :0 - %{p%}: ] | :%' ]* _ ; 
      where Cx in ( %{D%} %{C%} ) 
            Cy in (   t     ç   ) 
      matched ; 

!%{D%}:t <=> [ %{p%}: | :CnsDev ] (%':)  %>: _ ; 
!            [ %{p%}: | :CnsDev ]  %{v%}: (%':)  %>: _ ; 

! ağaç>{D}{A}n      = ağaçtan   = a:a ğ:ğ a:a ç:ç >:0 {D}:t {A}:a n:n
! fıstık>{D}{A}n    = fıstıktan = 
! şiş>{D}{A}n       = şişten    =
! gunah>{D}{A}n     = gunahtan  =
! kebap>{D}{A}n     = kebaptan  = 
! et{·}>{D}{I}      = etti      = e:e t:t %{v%}:0 >:0 {D}:t {I}:i


! ev>{s}{I}         = evi       =     e:e v:v >:0 {s}:0 {I}:i
! akü>{N}{I}n       = akünün    = a:a k:k ü:ü >:0 {N}:n {I}:ü n:n

"Epenthetic consonants after vowel"
Cx:Cy <=>  [ [ AbstrVow: - %{fi%}: ] | :SurVow ] (%':) (:0) %>: _ ; 
           [ [ AbstrVow: - %{fi%}: ] | :SurVow ]    %-: %>: _ ;
        except
           %{A%}: %{z%}:0 %>: _ %{I%}: m ;
                          %>: _ .#. ; ! git<v><iv><ger><nom>+i<cop><aor><p3><sg> : git{v}>m{A}>{y} : gitme
                          %>: _ %>: l %{A%}: r ;  ! ner>ede>{y}>l{A}r 
        where
            Cx in ( %{N%} %{s%} %{y%} ) 
            Cy in (   n     s     y   ) 
        matched ; 

! ev>{s}{I}         = evi      =      e:e v:v >:0 {s}:0 {I}:i
! ada>{s}{I}        = adası    =  a:a d:d a:a >:0 {s}:s {I}:ı
! adam>{N}{I}n      = adamın   = 
! akü>{N}{I}n       = akünün   = 
! kitap>{y}{I}      = kitabı   = 
! anne>{y}{I}       = anneyi   = 
! Sofya'>y{I}       = Sofya'yı 
! ner>ede>{y}>l{A}r = neredeler 
!@ 9:9 2:2 {ii}:0 %:% -:- >:0 {y}:y {I}:i

"Prevocalic consonant voicing"
!Cx:Cy <=> :Vow (:0) _ %{v%}: %>: (:0) :SurVow ;
!Cx:Cy <=> _ %{v%}:0/[ :0 - %{v%}:0 ] :SurVow ;
! because the immediately above thing doesn't seem to work right,
!   you need to put %{v%} RIGHT AFTER the consonant that voices:
!   no other :0 stuff can intervene
Cx:Cy <=> _ %{v%}:0 [ :0 - %{v%}:0 ]* :SurVow ;
          except
                 :n _ %{v%}: %>: (:0) :SurVow ;
          where Cx in ( t k ç p g  )
                Cy in ( d ğ c b ğ   )
                matched;

! kitab>            = kitap
! kitab>{D}{A}n     = kitaptan   = 

"Alternative voicing of 'k' to 'g' after 'n'"
k:g <=> :n _ %{v%}: %>: (:0) :Vow ;

! renk>{y}{I}       = rengi    =

"Buffer N after 3rd singular possessive + acc/dat/loc/abl"
%{n%}:n <=>  _ %>: [ %{y%}: %{I%}: | %{y%}: %{A%}: | %{D%}: %{A%}: ] ; 

! arka>{s}{I}{n}>{D}{A}     = arkasında = a:a r:r k:k a:a >:0 {s}:s {I}:ı {n}:n >:0 {D}:d {A}:a
! yap>m{A}>{s}{I}{n}>{y}{I} = yapmasını = y:y a:a p:p >:0 m:m {A}:a >:0 {s}:s {I}:ı {n}:n >:0 {y}:0 {I}:ı
! memelerinden
! arabasında

"Consonant alternation in passive"
%{L%}:n <=>  [ :Vow | :l ]  %>: _ ;
             [ :Vow | :l ]  %>: %{i%}: _ ; 
             [ %{e%}: | :Vow | :l ]  %>: %{i%}:i _ ; 

"Deletion of stem vowel before -{I}yor"
Vx:0 <=> _ %>: %{I%}: y:y o:o r:r  ;
       where Vx in ( a e i o u ı ö ü ) ;

"Deletion of %{A%} before -{I}yor and after a vowel"
%{A%}:0 <=> _ %>: %{I%}: y:y o:o r:r ;
 	        ! oyna>{I}yor    = oynuyor = o:o y:y n:n a:0 >:0 {I}:u y:y o:o r:r
           :Vow/[ :0 - [ AbstrCns: | AbstrVow: ] ]  _ ;
           ! başla>{A}y{I}m = başlayım

"Special treatment of {в} for su"
%{в%}:0 <=> _ .#. ; 
