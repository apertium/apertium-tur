!! List of people who will hate this file:
!!     1. Firespeakerwh

Alphabet

 A B C Ç D E F G Ğ H I İ J K L M N O Ö P Q R S Ş T U Ü V W X Y Z 
 a b c ç d e f g ğ h ı i j k l m n o ö p q r s ş t u ü v w x y z 

 %{A%}:a %{I%}:ı %{D%}:d

 %{n%}:0 %{y%}:0 %{s%}:0 %{i%}:0

 %>:0

 %+ %- %' % ;


Sets

SurVow = İ Ü E Ö I U A O
         i ü e ö ı u a o ; 


Vow    = İ Ü E Ö I U A O
         i ü e ö ı u a o
       %{A%} %{I%} ;

Cns    = B C Ç D F G Ğ H J K L M N P Q R S Ş T V W Y Z 
         b c ç d f g ğ h j k l m n p q r s ş t v w y z 
         %' ;

Rules 


"Epenthetic consonants after vowel"
Cx:Cy <=>   :SurVow %>: _ ; 
        where
            Cx in ( %{n%} %{s%} %{y%} ) 
            Cy in (   n     s     y   ) 
        matched ; 

! ev>{s}{I} = evi 
! ada>{s}{I} = adası
! adam>{n}{I}n = adamın
! akü>{n}{I}n = akünün
! kitap>{y}{I} = kitabı
! anne>{y}{I} = anneyi

"Prevocalic consonant voicing"
Cx:Cy <=> :Vow (:0) _ %>: (:0) :Vow ;
          except
                 :n _ %>: (:0) :Vow ;
          where Cx in ( p t k ç )
                Cy in ( b d ğ c )
                matched;

! kitap>{y}{I} = kitabı
! tat>{y}{I} = tadi
! fistik>{y}{I} = fistiği
! ağaç>{y}{I} = ağacı
! renk>{y}{I} = rengi

"Alternative voicing of 'k' to 'g' after 'n'"
k:g <=> :n _ %>: (:0) :Vow ;

! renk>{y}{I} = rengi

